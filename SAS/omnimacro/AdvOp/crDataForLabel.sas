%macro crDataForLabel(
	inDAT		=
	,outDAT		=
	,procLIB	=	WORK
);
%*000.	Info.;
/*-------------------------------------------------------------------------------------------------------------------------------------*\
|100.	Introduction.																													|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	|This macro is intended to extract the Labels of all variables of the given dataset, into a new dataset.							|
|---------------------------------------------------------------------------------------------------------------------------------------|
|200.	Glossary.																														|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	|inDAT		:	Input dataset.																										|
|	|outDAT		:	The output dataset, which has the same column name of the original one yet has its labels are one record.			|
|	|procLIB	:	The processing library.																								|
|---------------------------------------------------------------------------------------------------------------------------------------|
|300.	Update log.																														|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	| Date |	20131113		| Version |	1.00		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Version 1.																													|
|	|______|____________________________________________________________________________________________________________________________|
|	|___________________________________________________________________________________________________________________________________|
|	| Date |	20180310		| Version |	1.10		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Replace the macro expression [&aaa. = ] with [%length(%qsysfunc(compress(&aaa.,%str( )))) = 0] to avoid unnecessary			|
|	|      | failures.																													|
|	|______|____________________________________________________________________________________________________________________________|
|---------------------------------------------------------------------------------------------------------------------------------------|
|400.	User Manual.																													|
|---------------------------------------------------------------------------------------------------------------------------------------|
|---------------------------------------------------------------------------------------------------------------------------------------|
|500.	Dependent Macros.																												|
|---------------------------------------------------------------------------------------------------------------------------------------|
\*-------------------------------------------------------------------------------------------------------------------------------------*/

%*010.	Set parameters.;
%let	procLIB	=	%unquote(&procLIB.);
%if	%length(%qsysfunc(compress(&inDAT.,%str( ))))	=	0	%then	%let	inDAT		=	_last_;
%if	%length(%qsysfunc(compress(&outDAT.,%str( ))))	=	0	%then	%let	outDAT		=	_temp_;
%if	%length(%qsysfunc(compress(&procLIB.,%str( ))))	=	0	%then	%let	procLIB		=	WORK;

%*100.	Retrieve Labels.;
proc contents
	data=%unquote(&inDAT.)
	out=&procLIB..__cnt__
	noprint
;
run;

%*200.	Fix labels by field names.;
data &procLIB..__cnt_fix_;
	set &procLIB..__cnt__;
	if	missing(label)	then	label	=	cats(name);
run;

%*300.	Transpose.;
proc sort
	data=&procLIB..__cnt_fix_
;
	by	varnum;
run;
proc transpose
	data=&procLIB..__cnt_fix_
	out=%unquote(&outDAT.)(
		drop=
			_NAME_
			_LABEL_
	)
;
	id	name;
	var	label;
run;

%EndOfProc:
%mend crDataForLabel;