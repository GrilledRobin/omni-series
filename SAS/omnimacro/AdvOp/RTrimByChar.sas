%macro RTrimByChar(
	inVAR	=
	,inCHAR	=
	,outVAR	=
	,tmpVAR	=	_v_ltrim
	,tmpI	=	_tmp_i
);
%*000.	Info.;
/*-------------------------------------------------------------------------------------------------------------------------------------*\
|100.	Introduction.																													|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	|This macro invokes SAS data step to trim the trailing repeating characters of each observation of a given variable.				|
|---------------------------------------------------------------------------------------------------------------------------------------|
|200.	Glossary.																														|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	|inVAR	:	Input variable name.																									|
|	|inCHAR	:	The character to be right-trimmed.																						|
|	|outVAR	:	Output variable name.																									|
|	|tmpVAR	:	Interim variable name.																									|
|	|tmpI	:	Loop variable name.																										|
|---------------------------------------------------------------------------------------------------------------------------------------|
|300.	Update log.																														|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	| Date |	20130806		| Version |	1.00		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Version 1.																													|
|	|______|____________________________________________________________________________________________________________________________|
|	|___________________________________________________________________________________________________________________________________|
|	| Date |	20180310		| Version |	1.10		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Replace the macro expression [&aaa. = ] with [%length(%qsysfunc(compress(&aaa.,%str( )))) = 0] to avoid unnecessary			|
|	|      | failures.																													|
|	|______|____________________________________________________________________________________________________________________________|
|---------------------------------------------------------------------------------------------------------------------------------------|
|400.	User Manual.																													|
|---------------------------------------------------------------------------------------------------------------------------------------|
|---------------------------------------------------------------------------------------------------------------------------------------|
|500.	Dependent Macros.																												|
|---------------------------------------------------------------------------------------------------------------------------------------|
\*-------------------------------------------------------------------------------------------------------------------------------------*/

%*010.	Check parameters.;
%local
	L_mcrLABEL
;
%let	L_mcrLABEL	=	&sysMacroName.;
%if	%length(%qsysfunc(compress(&inVAR.,%str( ))))	=	0	%then %do;
	%put	%str(N)OTE: [&L_mcrLABEL.]Input field is not provided! Macro ended due to %str(e)rrors.;
	%goto	EndOfProc;
%end;
%if	%length(%qsysfunc(compress(&outVAR.,%str( ))))	=	0	%then	%let	outVAR	=	&inVAR._trimmed;
%if	%length(%qsysfunc(compress(&tmpVAR.,%str( ))))	=	0	%then	%let	tmpVAR	=	_v_ltrim;

%*100.	Make statements.;
length
	&tmpVAR.	$1.
;
&tmpI.	=	length(&inVAR.);
if &inVAR.	=	''	then	&outVAR.	=	'';
else do;
	&outVAR.	=	&inVAR.;
	&tmpVAR.	=	substr(&outVAR.,length(&outVAR.));
	do while (&tmpVAR.	=	"&inCHAR.");
		&tmpI.	+	(-1);
		if	&tmpI.	=	0	then do;
			&outVAR.	=	'';
			leave;
		end;
		&outVAR.	=	substr(&outVAR.,1,length(&outVAR.) - 1);
		&tmpVAR.	=	substr(&outVAR.,length(&outVAR.));
	end;
end;
drop &tmpI.;
%EndOfProc:
%mend RTrimByChar;