%macro chkDUPerr(
	inDAT	=
	,inKEY	=
	,dupDAT	=	tmp.__DUPchk
);
%*000.	Info.;
/*-------------------------------------------------------------------------------------------------------------------------------------*\
|100.	Introduction.																													|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	|This macro is intended to verify the duplicated observations in the provided dataset and issue an error mssage if any				|
|	| duplication is found.																												|
|---------------------------------------------------------------------------------------------------------------------------------------|
|200.	Glossary.																														|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	|inDAT	:	The data to be verified.																								|
|	|inKEY	:	The key variable(s) to check the duplication.																			|
|	|dupDAT	:	The dataset that contains the duplicated observations during the verification process									|
|---------------------------------------------------------------------------------------------------------------------------------------|
|300.	Update log.																														|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	| Date |	20140331		| Version |	1.00		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Version 1.																													|
|	|______|____________________________________________________________________________________________________________________________|
|	|___________________________________________________________________________________________________________________________________|
|	| Date |	20180310		| Version |	1.10		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Replace the macro expression [&aaa. = ] with [%length(%qsysfunc(compress(&aaa.,%str( )))) = 0] to avoid unnecessary			|
|	|      | failures.																													|
|	|______|____________________________________________________________________________________________________________________________|
|---------------------------------------------------------------------------------------------------------------------------------------|
|400.	User Manual.																													|
|---------------------------------------------------------------------------------------------------------------------------------------|
|---------------------------------------------------------------------------------------------------------------------------------------|
|500.	Dependent Macros.																												|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	|Below macros are from "&cdwmac.\AdvOp"																								|
|	|-----------------------------------------------------------------------------------------------------------------------------------|
|	|	|getOBS4DATA																													|
|	|	|ErrMcr																															|
\*-------------------------------------------------------------------------------------------------------------------------------------*/

%local
	L_mcrLABEL
	LfDUP
;
%let	L_mcrLABEL	=	&sysMacroName.;
%if	%length(%qsysfunc(compress(&dupDAT.,%str( ))))	=	0	%then	%let	dupDAT	=	tmp.__DUPchk;
data &dupDAT.;
	set _NULL_;
run;

proc sort
	data=%unquote(&inDAT.)
	out=__chkDUPbyKEY
	dupout=&dupDAT.
	nodupkey
;
	by &inKEY.;
run;

%let	LfDUP	=	0;
%getOBS4DATA(
	inDAT	=	&dupDAT.
	,outVAR	=	LfDUP
)

%if	%bquote(&LfDUP.)	>	0	%then %do;
	%put	%str(E)RROR: [&L_mcrLABEL.]Program found Duplicated KEY in [&inDAT.], will terminate immediately! Duplications have been output to [&dupDAT.];
	%ErrMcr
%end;
%mend chkDUPerr;