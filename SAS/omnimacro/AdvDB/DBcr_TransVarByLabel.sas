%macro DBcr_TransVarByLabel(
	inDAT		=
	,inVarDef	=
	,outDAT		=
	,procLIB	=	WORK
);
%*000.	Info.;
/*-------------------------------------------------------------------------------------------------------------------------------------*\
|100.	Introduction.																													|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	|This macro is intended to translate variables in the given data by their LABELS and the data dictionary which describes the		|
|	| attributes of these variables in the key as their LABELS.																			|
|	|One scenario to use this macro is the bulk import of the EXCEL reports generated by a system, with a bunch of standard variables	|
|	| that are named in Chinese, while we do not want to rename a sub list among all the possible variables in each import program.		|
|---------------------------------------------------------------------------------------------------------------------------------------|
|200.	Glossary.																														|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	|inDAT		:	The dataset that is imported by the IMPORT procedure.																|
|	|inVarDef	:	The META data that is used to translate the LABELS into standard SAS variable names.								|
|	|outDAT		:	The translated dataset.																								|
|	|procLIB	:	The working library.																								|
|---------------------------------------------------------------------------------------------------------------------------------------|
|300.	Update log.																														|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	| Date |	20161101		| Version |	1.00		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Version 1.																													|
|	|______|____________________________________________________________________________________________________________________________|
|	|___________________________________________________________________________________________________________________________________|
|	| Date |	20161108		| Version |	1.10		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Added the %superq function so that any special characters in the LABEL contents can be masked.								|
|	|______|____________________________________________________________________________________________________________________________|
|	|___________________________________________________________________________________________________________________________________|
|	| Date |	20170408		| Version |	1.20		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Added verification if there are missing values in the data, otherwise the program would issue errors on INPUT and PUT		|
|	|      | functions.																													|
|	|______|____________________________________________________________________________________________________________________________|
|	|___________________________________________________________________________________________________________________________________|
|	| Date |	20170815		| Version |	1.30		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Quote the LABEL during calling SYMPUTX routine and translate the single-quotation marks into double ones, in case there are	|
|	|      | special characters in the LABEL that should be masked.																		|
|	|______|____________________________________________________________________________________________________________________________|
|	|___________________________________________________________________________________________________________________________________|
|	| Date |	20180304		| Version |	1.40		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Replace the macro expression [&aaa. = ] with [%length(%qsysfunc(compress(&aaa.,%str( )))) = 0] to avoid unnecessary			|
|	|      | failures.																													|
|	|______|____________________________________________________________________________________________________________________________|
|	|___________________________________________________________________________________________________________________________________|
|	| Date |	20180615		| Version |	1.50		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Remove the call of function [STRIP] from within the function [INPUT], for the format that is to be applied to the input		|
|	|      | value can only recognize the expression instead of a muted value.															|
|	|      |Eg. [input(var,usf.)] is correct, but [input(strip(var),usf.)] will report %str(e)rror message.								|
|	|______|____________________________________________________________________________________________________________________________|
|	|___________________________________________________________________________________________________________________________________|
|	| Date |	20180722		| Version |	1.60		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Leverage the additional option field of the function [QUOTE] to eliminate unexpected results.								|
|	|______|____________________________________________________________________________________________________________________________|
|	|___________________________________________________________________________________________________________________________________|
|	| Date |	20201106		| Version |	1.70		| Updater/Creator |	Lu Robin Bin												|
|	|______|____________________|_________|_____________|_________________|_____________________________________________________________|
|	| Log  |Add verification of missing values on the fields: [FORMATL],[FORMATD],[INFORML] and [INFORMD] to enable compatibility to	|
|	|      | the formats WITHOUT limit on the formatted length and decimals as well as the in-formats WITHOUT the same limit.			|
|	|      |This is useful when applying user defined format or in-format.																|
|	|______|____________________________________________________________________________________________________________________________|
|---------------------------------------------------------------------------------------------------------------------------------------|
|400.	User Manual.																													|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	|1. Supposedly the headers in the EXCEL file only contain Chinese characters, then the IMPORT procedure automatically creates		|
|	|    variables in the form of [_COL<n>], with their labels as in these Chinese characters.											|
|	|-----------------------------------------------------------------------------------------------------------------------------------|
|	|2. Setup a META data, or [inVarDef] in below format (same as the output of CONTENTS procedure), where LABEL has the same value as	|
|	|    in above IMPORT data:																											|
|	|	|LABEL		$256.		(Should be the same as the header row in the raw source file)											|
|	|	|NAME		$32.		(Should be the SAS variable name to be defined)															|
|	|	|TYPE		best12.		(1 or 2)																								|
|	|	|LENGTH		best12.																												|
|	|	|FORMAT		$32.																												|
|	|	|FORMATL	best12.																												|
|	|	|FORMATD	best12.																												|
|	|	|INFORMAT	$32.		(Should be the same as observed in the raw source file)													|
|	|	|INFORML	best12.		(Should be the same as observed in the raw source file)													|
|	|	|INFORMD	best12.		(Should be the same as observed in the raw source file)													|
|	|You could collect all the possible Chinese variable names and setup them in this META data for future use.							|
|	|-----------------------------------------------------------------------------------------------------------------------------------|
|	|3. Run this macro upon the IMPORT data, in terms of the META data, to translate the variables as the predefined [NAME].			|
|---------------------------------------------------------------------------------------------------------------------------------------|
|500.	Dependent Macros.																												|
|---------------------------------------------------------------------------------------------------------------------------------------|
|	|Below macros are from "&cdwmac.\AdvOp"																								|
|	|-----------------------------------------------------------------------------------------------------------------------------------|
|	|	|getOBS4DATA																													|
|	|	|genvarlist																														|
\*-------------------------------------------------------------------------------------------------------------------------------------*/

%*010.	Set parameters.;
%*011.	Identify current processing macro.;
%local
	L_mcrLABEL
	Lohno
;
%let	L_mcrLABEL	=	&sysMacroName.;
%let	Lohno		=	%str(E)RROR: [&L_mcrLABEL.]Process failed due to %str(e)rrors!;

%*012.	Handle the parameter buffer.;
%let	procLIB	=	%unquote(&procLIB.);
%if	%length(%qsysfunc(compress(&inDAT.,%str( ))))		=	0	%then %do;
	%put	%str(W)ARNING: [&L_mcrLABEL.]No input data is specified! Program stopped!;
	%goto	EndOfProc;
%end;
%if	%length(%qsysfunc(compress(&inVarDef.,%str( ))))	=	0	%then %do;
	%put	%str(W)ARNING: [&L_mcrLABEL.]No Definition data is specified! Program stopped!;
	%goto	EndOfProc;
%end;
%if	%length(%qsysfunc(compress(&outDAT.,%str( ))))		=	0	%then %do;
	%put	%str(W)ARNING: [&L_mcrLABEL.]No output data is specified! Program stopped!;
	%goto	EndOfProc;
%end;
%if	%length(%qsysfunc(compress(&procLIB.,%str( ))))		=	0	%then	%let	procLIB	=	WORK;

%*013.	Define the global environment.;

%*014.	Define the local environment.;
%local
	LerrFldLst
	Vi
;
%let	LerrFldLst	=;

%*100.	Read the contents of the IMPORT data.;
proc contents
	data=%unquote(&inDAT.)
	out=&procLIB.._TVBL_in
	noprint
;
run;

%*200.	Define the variables to be translated by their labels.;
proc sql;
	create table &procLIB.._TVBL_def as (
		select
			a.LABEL
			,a.NAME
			,a.TYPE
			,a.LENGTH
			,a.FORMAT
			,a.FORMATL
			,a.FORMATD
			,a.INFORMAT
			,a.INFORML
			,a.INFORMD
			,a.VARNUM
			,b.LABEL as new_LABEL
			,b.NAME as new_NAME
			,b.TYPE as new_TYPE
			,b.LENGTH as new_LENGTH
			,b.FORMAT as new_FORMAT
			,b.FORMATL as new_FORMATL
			,b.FORMATD as new_FORMATD
			,b.INFORMAT as new_INFORMAT
			,b.INFORML as new_INFORML
			,b.INFORMD as new_INFORMD
			,missing(b.LABEL) as F_err_missingMeta
		from &procLIB.._TVBL_in as a
		left join %unquote(&inVarDef.) as b
			on	a.LABEL	=	b.LABEL
	)
	order by
		VARNUM
	;
quit;

%*300.	Issue error when the data is verified as invalid.;
%*310.	When some fields are not found in the META data.;
%if %getOBS4DATA( inDAT = %nrbquote( &procLIB.._TVBL_def( where=( F_err_missingMeta ) ) ) , gMode = F ) %then %do;
	%*100.	Prepare the error field list.;
	proc sql noprint;
		select	LABEL
		into	:LerrFldLst	separated by ";"
		from &procLIB.._TVBL_def( where=( F_err_missingMeta ) )
		;
	quit;
	%let	LerrFldLst	=	%qsysfunc(strip(%superq(LerrFldLst)));

	%*900.	Abort the process.;
	%put	%str(W)ARNING: [&L_mcrLABEL.]Some fields need to be translated but DO NOT exist in the META data!;
	%put	%str(W)ARNING: [&L_mcrLABEL.]Field List: [&LerrFldLst.];
	%ErrMcr
%end;

%*400.	Prepare the statements to translate the variables.;
data _NULL_;
	%*001.	Set the data.;
	set &procLIB.._TVBL_def end=EOF;
	by VARNUM;

	%*100.	Call necessary macro variables.;
	call symputx(cats("LoVName",_N_),NAME,"L");
	call symputx(cats("LoVType",_N_),TYPE,"L");
	call symputx(cats("LoVLen",_N_),LENGTH,"L");
	call symputx(cats("LnVName",_N_),new_NAME,"L");
	call symputx(cats("LnVLabel",_N_),quote(strip(new_LABEL),"'"),"L");
	call symputx(cats("LnVType",_N_),new_TYPE,"L");
	call symputx(cats("LnVLen",_N_),new_LENGTH,"L");
	call symputx(cats("LnVFmtLen",_N_),ifc(new_TYPE=1,min(8,max(3,LENGTH,new_LENGTH)),cats(new_FORMAT,new_LENGTH)),"L");
	call symputx(cats("LnVFmt",_N_),cats(new_FORMAT,ifc(missing(new_FORMATL),"",new_FORMATL),".",ifc(missing(new_FORMATD),"",new_FORMATD)),"L");
	call symputx(cats("LnVInFmt",_N_),cats(new_INFORMAT,ifc(missing(new_INFORML),"",new_INFORML),".",ifc(missing(new_INFORMD),"",new_INFORMD)),"L");
	if	EOF	then do;
		call symputx("LkVar",_N_,"L");
	end;
run;

%*500.	Issue error when the fields are verified as invalid.;
%*510.	When the new name is the same as the old one for the same LABEL.;
%do Vi=1 %to &LkVar.;
	%if	&&LoVName&Vi..	=	&&LnVName&Vi..	%then %do;
		%*100.	When the new LENGTH is smaller than the old one, we issue a warning of possible truncation.;
		%if	&&LoVLen&Vi..	>	&&LnVLen&Vi..	%then %do;
			%put	%str(W)ARNING: [&L_mcrLABEL.]Length of [&&LnVLabel&Vi..] could be truncated due to short variable definition.;
		%end;

		%*200.	When the old TYPE is different from the new TYPE, we abort the process.;
		%if	&&LoVType&Vi..	^=	&&LnVType&Vi..	%then %do;
			%put	%str(W)ARNING: [&L_mcrLABEL.][&&LnVLabel&Vi..] is defined as both Character and Numeric for standard import!;
			%put	%str(W)ARNING: [&L_mcrLABEL.]Check [&&LoVName&Vi..] in [&inDAT.] and [&inVarDef.]!;
			%ErrMcr
		%end;
	%end;
%end;

%*600.	Translation.;
data %unquote(&outDAT.);
	%*100.	Initialize all variables.;
	attrib
	%do Vi=1 %to &LkVar.;
		&&LnVName&Vi..
			length	=	&&LnVFmtLen&Vi..
			label	=	&&LnVLabel&Vi..
		%if	%length(%qsysfunc(compress(&&LnVFmt&Vi..,%str( ))))	^=	0	%then %do;
			format	=	&&LnVFmt&Vi..
		%end;
	%end;
	;

	%*200.	Set the IMPORT data.;
	set %unquote(&inDAT.);

	%*300.	Translation.;
	%do Vi=1 %to &LkVar.;
		%if	&&LoVType&Vi..	=	&&LnVType&Vi..	%then %do;
			&&LnVName&Vi..	=	&&LoVName&Vi..;
		%end;
		%else %do;
			%if	&&LnVType&Vi..	=	1	%then %do;
				if	missing(&&LoVName&Vi..)	=	0	then do;
					&&LnVName&Vi..	=	input(&&LoVName&Vi..,&&LnVInFmt&Vi..);
				end;
			%end;
			%else %do;
				if	missing(&&LoVName&Vi..)	=	0	then do;
					&&LnVName&Vi..	=	strip(put(&&LoVName&Vi..,&&LnVInFmt&Vi..));
				end;
			%end;
		%end;
	%end;

	%*900.	Purge.;
	keep
	%do Vi=1 %to &LkVar.;
		&&LnVName&Vi..
	%end;
	;
run;

%EndOfProc:
%mend DBcr_TransVarByLabel;

/*-Notes- -Begin-* /
%*Full Test Program[1]:;
options
	sasautos=(
		sasautos
		"D:\SAS\omnimacro\AdvDB"
		"D:\SAS\omnimacro\AdvOp"
	)
	mautosource
;
%*Prevent the program from being bombed.;
%macro ErrMcr;
%mend ErrMcr;

PROC IMPORT
	OUT			=	testRaw
	DATAFILE	=	"D:\SAS\omnimacro\AdvDB\test_DBcr_TransVarByLabel.xlsx"
	DBMS		=	EXCEL2010
	REPLACE
;
	SHEET		=	"sheet1$";
	GETNAMES	=	YES;
	MIXED		=	NO;
	SCANTEXT	=	YES;
	USEDATE		=	YES;
	SCANTIME	=	YES;
RUN;

data def;
	format
		LABEL		$256.
		NAME		$32.
		TYPE		best12.
		LENGTH		best12.
		FORMAT		$32.
		FORMATL		best12.
		FORMATD		best12.
		INFORMAT	$32.
		INFORML		best12.
		INFORMD		best12.
	;
	LABEL		=	"×Ö¶Î1";
	NAME		=	"var1";
	TYPE		=	2;
	LENGTH		=	8;
	FORMAT		=	"$";
	FORMATL		=	"16";
	FORMATD		=	0;
	INFORMAT	=	"$";
	INFORML		=	8;
	INFORMD		=	0;
	output;

	LABEL		=	'×Ö¶Î2 %pa';
	NAME		=	"var2";
	TYPE		=	1;
	LENGTH		=	8;
	FORMAT		=	"COMMA";
	FORMATL		=	"12";
	FORMATD		=	2;
	INFORMAT	=	"COMMA";
	INFORML		=	12;
	INFORMD		=	0;
	output;

	LABEL		=	"×Ö¶Î4";
	NAME		=	"var3";
	TYPE		=	2;
	LENGTH		=	8;
	FORMAT		=	"$";
	FORMATL		=	"8";
	FORMATD		=	0;
	INFORMAT	=	"COMMA";
	INFORML		=	12;
	INFORMD		=	2;
	output;

	LABEL		=	"×Ö¶Î5";
	NAME		=	"var5";
	TYPE		=	1;
	LENGTH		=	8;
	FORMAT		=	"BEST";
	FORMATL		=	"12";
	FORMATD		=	0;
	INFORMAT	=	"COMMA";
	INFORML		=	12;
	INFORMD		=	2;
	output;
run;

%DBcr_TransVarByLabel(
	inDAT		=	testRaw
	,inVarDef	=	def
	,outDAT		=	new
	,procLIB	=	WORK
)

%*Output.;

/*-Notes- -End-*/